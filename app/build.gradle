plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'antlr'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.13.1"
    // Use TestNG framework, also requires calling test.useTestNG() below
    testImplementation libs.testng
    implementation 'com.kazurayam:unittest-helper:0.5.0'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


tasks.named('test') {
    // Use TestNG for unit tests.
    useTestNG()
}

generateGrammarSource {
    arguments += [
            // -lib option is required so that the LBExpression grammar can import LBTokens grammar.
            // without -lib, ANTLR4 will fail to find the LBTokens.g4 file
            "-lib", "src/main/antlr/basic",
            // -package option is required so that the generated *.java files to have the "package basic;" statement
            "-package", "basic",
            // --visitor option is required to generate the "basic/LittleBasicBaseVisitor" class
            "-visitor",
            "-long-messages"]
    maxHeapSize = "64m"
}

jar {
    manifest {
        attributes 'Main-Class': 'demo.App'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
